<version> ::= "TAP version 14\n"

<test> ::= <header> <test-set>

<header> ::= <comment>* <version> <comment>*

<comment> ::= "#" <text>* "\n"

<test-set> ::= <plan> <test-point>* [<bailout>]
             | <test-point>* <bailout>
             | <test-point>* <plan>

<bailout> ::= "Bail out!" [" " <text>*] "\n"

<plan> ::= <integer> ".." <integer> "\n"

<test-point> ::= [<child-test>] <assert-line> [<diags>]

<assert-line> ::= <passing> [" " <integer>] <text> [<directive>] "\n"

<directive> ::= <skip> | <todo> | <time>

<skip> ::= "#" " "* <skip-word> [<text>]

<todo> ::= "#" " "* <todo-word> [<text>]

<skip-word> ::= "SKIP" | "skip"

<todo-word> ::= "TODO" | "todo"

<time> ::= "#" " "* "time=" <number> <time-unit>

<time-unit> ::= "s" | "ms"

<integer> ::= "0" | <non-zero-digit> <digit>*

<number> ::= <integer> ["." <digit>*]

<non-zero-digit> ::= "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"

<digit> ::= "0" | <non-zero-digit>

<diags> ::= "  ---\n" <yaml>* "  ...\n"

<yaml> ::= "  " <yaml-line> "\n"

<child-test> ::= "    # Subtest: " <text> "\n" INDENT("    ", <test-set>)

